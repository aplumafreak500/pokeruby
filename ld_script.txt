ENTRY(Start)

gNumMusicPlayers = 4;
gMaxLines = 60;

SECTIONS {
	. = 0x2000000;
	ewram (NOLOAD) :
	ALIGN(4)
	{
		INCLUDE "sym_ewram.ld"
		*(SORT(ewram_data));
		. = 0x40000;
	}

	. = 0x3000000;
	iwram (NOLOAD) :
	ALIGN(4)
	{
		/* .bss starts at 0x3000000 */
		*(SORT(.bss*) SORT(iwram_data) SORT(COMMON));
		src/libs/m4a.o(.bss.code);
		end = .;
	. = 0x8000;
	}

	. = 0x8000000;
	.text :
	ALIGN(4)
	{
		src/crt0.o(.text);
		*(SORT(.text*));
	} =0

	script_data :
	ALIGN(4)
	{
		*(SORT(script_data));
	} =0

	.data :
	ALIGN(4)
	{
		*(SORT(.data*));
	} =0

	.rodata :
	ALIGN(4)
	{
		*(SORT(.rodata*));
	} =0

	.ARM.exidx : { *(.ARM.exidx) }=0
	.ARM.extab : { *(.ARM.extab) }=0

	.comment 0 : { *(.comment) }
	.ARM.attributes 0 : { *(.ARM.attributes) }

	/* DWARF 2 sections */
	.debug_ranges   0 : { *(.debug_ranges) }
	.debug_aranges  0 : { *(.debug_aranges) }
	.debug_pubnames 0 : { *(.debug_pubnames) }
	.debug_info     0 : { *(.debug_info) }
	.debug_abbrev   0 : { *(.debug_abbrev) }
	.debug_line     0 : { *(.debug_line) }
	.debug_frame    0 : { *(.debug_frame) }
	.debug_str      0 : { *(.debug_str) }
	.debug_loc      0 : { *(.debug_loc) }
	.debug_macinfo  0 : { *(.debug_macinfo) }
	.debug_macro    0 : { *(.debug_macro) }

	/* Discard everything not specifically mentioned above. */
	/DISCARD/ :
	{
		*(*);
	}
}
